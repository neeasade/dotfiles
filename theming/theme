#!/usr/bin/env bb
;; -*- mode: clojure -*-

(require '[clojure.java.shell :as shell]
         '[clojure.string :as string]
         '[clojure.tools.cli :as cli]
         '[selmer.parser :as template]
         '[clojure.walk :refer [postwalk prewalk]])

(defn get-env []
  (->> (System/getenv)
       (map (fn [[k v]] {(keyword k) v}))
       (into {})))

(defn deep-merge [a & maps]
  (if (map? a)
    (apply merge-with deep-merge a maps)
    (apply merge-with deep-merge maps)))

(defn flatten-map
  "{:a {:b 1}} -> {:a.b 1}"
  ([m] (flatten-map m "."))
  ([m separator]
   (into {} (flatten-map m separator nil)))
  ([m separator pre]
   (mapcat (fn [[k v]]
             (let [prefix (if pre (str pre separator (name k)) (name k))]
               (if (map? v)
                 (flatten-map v separator prefix)
                 [[(keyword prefix) v]])))
           m)))

(defn unflatten-map
  "{:a.b 1} -> {:a {:b 1}}"
  ([m] (unflatten-map m #"\."))
  ([m separator-re]
   (reduce
    (fn [m [k v]]
      (let [keyword-path (map keyword (string/split (name k) separator-re))]
        (assoc-in m keyword-path v)))
    {} m)))

(defn index-of [coll to-find]
  (first
   (keep-indexed
    (fn [i v]
      (when (= v to-find)
        i))
    coll)))

(defn panel-format-to-lemons [format]
  ;; "a:b|c:d|e" -> [{:keys [name step end end-inner align]}]
  (let [parts (string/split format #"\|")
        all (-> format (string/split #"\||:"))
        transform (fn [part-fn align]
                    (->> (-> parts part-fn (string/split #":"))
                         (map (fn [name] {:name name
                                          :end false
                                          :end-inner false
                                          :align align
                                          :step (index-of all name)
                                          }))
                         (into [])))
        lemons-left (transform first 'left)
        lemons-center (transform second 'center)
        lemons-right (transform #(nth % 2) 'right)]
    (concat
     (-> lemons-left
         (update-in [0] assoc :end true)
         (update-in [(dec (count lemons-left))] assoc :end-inner true))
     (-> lemons-center
         (update-in [0] assoc :end-inner true)
         (update-in [(dec (count lemons-center))] assoc :end-inner true))
     (-> lemons-right (update-in [0] assoc :end-inner true)
         (update-in [(dec (count lemons-right))] assoc :end true)))))

(defn panel-color-lemons
  "give lemons: bg fg line"
  [lemons]
  (let [vanilla (fn [lemon]
                  (-> lemon
                      (assoc :bg "#ffffff")
                      (assoc :fg "#000000")
                      (assoc :line "#000000")))]
    (map vanilla lemons)))

(defn panel-lemons-to-i3blocks [lemons]
  (let [lemon-info {:interval {
                               :network 20
                               :mail 30
                               :desktop "bspc subscribe"
                               :tags "persist"
                               :title "persist"
                               :weather 600
                               :updates 600
                               :github 120}
                    :mouse {
                            ;; lemon name
                            :mpd {:left "player.sh toggle"
                                  :right "player.sh next"
                                  :scrolldown "mpc seek -00:00:02"
                                  :scrollup "mpc seek +00:00:02"}
                            :volume {:scrollup "ponymix decrease 5"
                                     :scrolldown "ponymix increase 5"}
                            ;; :org-task {:left "elisp '(org-clock-goto)'; bspc node -f \$(xdotool search --onlyvisible --class Emacs)"
                            ;;            :right "elisp org-clock-current-task || bspc node -f \$(xdotool search --onlyvisible --class Emacs); elisp '(org-pomodoro)'"
                            ;;            }
                            }
                    }]
    (->> lemons
         (map (fn [lemon]
                (merge
                 (select-keys lemon [:bg :fg :line :step :align])
                 {:lemon (:name lemon)
                  :command (format "%s/.wm/panel/lemons/%s | season" (System/getenv "HOME") (:name lemon))
                  :lemon_end (:end lemon)
                  :lemon_end_inner (:end-inner lemon)
                  :interval (or ((-> lemon :name keyword) (:interval lemon-info))
                                5)}
                 (flatten-map
                  {:mouse ((-> lemon :name keyword) (:mouse lemon-info))}
                  "_"))))
         (map (fn [lemon]
                (->> lemon
                     (remove (comp nil? val))
                     (into {})))))))

(defn map-to-shell
  [m]
  (->> (flatten-map m "_")
       (map (fn [[k v]]
              (format "%s='%s'"
                      (-> k name (string/replace "-" "_"))
                      ;; todo: handle strings with "'"s in them
                      ;; sed "s#'#\'\\\'\'#g"
                      ;; (string/replace v "'" "\'\\\'\'")
                      v
                      )))
       (string/join "\n")))

(defn narrow [string-path m]
  (->> (string/split string-path #"\.")
       (map keyword)
       (get-in m)))

(defn promote [string-path m]
  (merge m (narrow string-path m)))

(defn str->int [str]
  (when (re-matches (re-pattern "\\d+") str)
    (read-string str)))

(defn missing-value-fn [tag context-map]
  (clojure.pprint/pprint
   (flatten-map (dissoc context-map :env)))
  (throw (Exception. (str "<Missing value: " (or (:tag-value tag) (:tag-name tag)) ">"))))

(defn retain-value-fn [tag context-map]
  (format "{{%s}}" (:tag-value tag)))

(selmer.util/set-missing-value-formatter! missing-value-fn)

(defn exec-fns [tree]
  ;; call things that are functions in a tree
  (postwalk
   (fn [n]
     (if (map? n)
       (->> n
            (map (fn [[k v]]
                   {k (if (fn? v)
                        (try
                          (v tree)
                          (catch Exception _
                            ;; expected to be failed get-in tree (because of a pending fn render)
                            v))
                        v)}))
            (into {}))
       n)) tree))

(defn render-leaves-with-siblings [tree]
  (prewalk
   (fn [n]
     (if (and (map? n)
              (not-any? map? (keys n)))
       ;; sibling render pass
       (into {}
             (map (fn [[k v]]
                    {k (if (and (string? v)
                                (string/includes? v "{{"))
                         (let [
                               ;; let top level map win conflicts
                               render-conf (-> tree
                                               (merge (apply dissoc n (keys tree)))
                                               (assoc :env (get-env)))
                               render-result (template/render v render-conf)]
                           ;; this is what allows us to make multiple passes:
                           (if (string/includes? render-result "{{")
                             v
                             render-result
                             ;; (if-let [x (str->int render-result)]
                             ;;   x
                             ;;   render-result)
                             ))
                         v)}) n))
       n)) tree))

;; theme build is:
;; resolve fn's in base-conf
;; deep-merge host themes
;; render string leaves in tree
;; remove keys ending in '-' from tree
(defn get-theme []
  (let [
        home? (= "erasmus" (.. java.net.InetAddress getLocalHost getHostName))
        work? false

        emacs-colors (let [result (shell/sh "elisp" "-r" "(ns/emacs-to-theme)")]
                       (when (= 0  (:exit result))
                         (read-string (:out result))))

        colors {:colors ["#E8EBEC" "#006E96" "#007C00" "#C38418" "#0065C8" "#407EE7" "#C6007F" "#444748" "#6A6D6E" "#006E96" "#007C00" "#C38418" "#0065C8" "#407EE7" "#C6007F" "#444748"],
                :color {:focused {:background "#a4cfed", :foreground "#093553", :faded "#305b79", :primary "#ac0065", :assumed "#0053b6", :alt "#005e86", :strings "#006c00"}, :normal {:background "#e8ebec", :foreground "#444748", :faded "#6a6d6e", :primary "#c6007f", :assumed "#0065c8", :alt "#006e96", :strings "#007c00"}, :weak {:background "#d5d6d7", :foreground "#393a3b", :faded "#5f6061", :primary "#ba0073", :assumed "#005dc0", :alt "#00668e", :strings "#007400"}, :strong {:background "#ccc9ca", :foreground "#343132", :faded "#5a5758", :primary "#ac0065", :assumed "#0053b6", :alt "#005e86", :strings "#006c00"}, :cursor "#0065c8"}}

        base-conf
        {:meta
         ;; for panel _and_ compositor
         {:shadow false}

         :font (fn [_]
                 (->> {:variable {:family "Droid Sans" :size 12}
                       :mono {:family "Droid Sans Mono" :size 12}
                       :icon {:family "Font Awesome 5 Free" :size 10}
                       :panel {:family "{{font.variable.family}}"
                               :size "{{font.variable.size}}"}}
                      (map (fn [[k v]]
                             {k (assoc v
                                       :spec "{{family}}-{{size}}"
                                       :spec-space "{{family}} {{size}}")}))
                      (into {})))

         :bspwm {:window-gap 10
                 :split-ratio 0.5
                 :desktop-names "00 01 10 11"
                 :monocle-window-percent 0.53
                 :bspwmrc-extend ""
                 :border {:width 3
                          :normal "{{colors.0}}"
                          :active "{{colors.8}}"
                          :focused "{{colors.7}}"
                          :presel "{{colors.8}}"}}

         :mkb {:bar- "-+-"
               :complete (fn [tree] (-> tree (get-in [:mkb :bar-]) (nth 2) (str)))
               :sep (fn [tree] (-> tree (get-in [:mkb :bar-]) (nth 1) (str)))
               :empty (fn [tree] (-> tree (get-in [:mkb :bar-]) (nth 0) (str)))
               :start ""
               :end  ""}
         :gtk {:theme "Raleigh"}
         ;; see: https://github.com/gnotclub/xst/blob/master/.Xresources
         :st {
              ;; :font "{{font.mono.spec}}"
              :cursorshape 2
              :cursorthickness 2
              :borderpx 0
              :depth 24}

         :shell {:prompt "$"
                 :prompt_err "!"}
         :x {:padding 4}
         :picom {:frame {:opacity 1.0}
                 :shadow {:opacity 0.4
                          :enabled false
                          :radius 1
                          :offset-x 1
                          :offset-y 1
                          :red 0.0
                          :blue 0.0
                          :green 0.0}
                 :fade {:enabled false
                        :in-step 0.1
                        :out-step 0.1}}
         :mpd (when home?
                (fn [_]
                  (if (string/includes?
                       (:out (shell/sh "mount"))
                       (format "%s/usb" (System/getenv "HOME")))
                    {:music-dir "{{env.HOME}}/usb/Music"
                     :mpd-dir "{{env.HOME}}/.config/mpd_portable"}
                    {:music-dir "{{env.HOME}}/Music"
                     :mpd-dir "{{env.HOME}}/.config/mpd"})))
         :panel {:height 10
                 :format  "dropdown:title|org_task:misc|mpd:volume:weather:clock"
                 :window-class (fn [tree]
                                 (if (get-in tree [:meta :shadow])
                                   "lemonbar" "noshadow"))
                 :gap 0
                 :position "top"
                 :position-flipped "bottom"
                 :top true
                 :lemons (fn [tree]
                           (->> (get-in tree [:panel :format])
                                (panel-format-to-lemons)
                                (panel-color-lemons )
                                (panel-lemons-to-i3blocks)))
                 }
         :qutebrowser (unflatten-map
                       {:statusbar nil
                        :tab.font.family "{{font.variable.family}}"
                        :tab.font.size
                        "{{font.panel.size}}"
                        ;; will this work?
                        :statusbar.font.size
                        ;; "{{qutebrowser.tab.font.size}}"
                        "{{font.panel.size}}"
                        :tab.bold (fn [tree]
                                    (let [panel-font (get-in tree [:font :panel :spec])
                                          bold? (string/includes? panel-font "Bold")]
                                      (if bold? "True" "False")))
                        })
         }
        home-theme (when home?
                     (unflatten-map
                      {:THEME_NAME "test"
                       :emacs.theme "tarp-mcfay"
                       :BG_COMMAND "${HOME}/.fehbg"
                       ;; :BG_COMMAND "hsetroot -solid '{{color.focused.background}}'"
                       :font.mono.family "Triplicate T4c"
                       :font.variable.family "Equity Text B"
                       :mkb.bar- "AAH"
                       :shell.prompt "%"
                       :bspwm.window-gap 40
                       :x.padding 5
                       :bspwm.monocle-window-percent 0.55
                       :font.mono.size 12
                       :font.variable.size 13
                       :picom.shadow.enabled "{{meta.shadow}}"
                       :bspwm.bspwmrc-extend "tag_borders &\n subscription_rules &"}))

        work-theme (when work?
                     (unflatten-map
                      {:THEME_NAME "test"
                       :emacs.theme "tarp-mcfay"
                       :BG_COMMAND "wallmac tile '{{env.HOME}}/Downloads/mpv-shot0095.jpg'"

                       :font.mono.family "Go Mono"
                       :font.variable.family "Charter"
                       :font.mono.size 16
                       :font.variable.size 16

                       :mkb.bar- ["ðŸŒ³" "ðŸŒ»" "ðŸŒ±"]
                       :shell.prompt "ðŸŒ³"
                       :shell.prompt_err "ðŸ˜ž"
                       :bspwm.window-gap 0
                       :x.padding 5
                       :bspwm.monocle-window-percent 0.55}))
        theme-tree (deep-merge
                    (-> base-conf exec-fns exec-fns)
                    (or emacs-colors colors)
                    home-theme work-theme)]

    (as-> theme-tree tree

      ;; unsure how many passes to do
      (render-leaves-with-siblings tree)
      (render-leaves-with-siblings tree)

      ;; remove keys ending in '-'
      (postwalk
       (fn [n]
         (if (map? n)
           (apply dissoc n (filter (fn [k] (string/ends-with? (pr-str k) "-"))
                                   (keys n)))
           n)) tree))))

(def cli-options
  [["-q" "--query PATH" "query path"]
   ["-p" "--promote PATH" "promote path"]
   ["-n" "--narrow PATH" "narrow map to path"]
   ["-R" "--render STRING" "render a string as a template"]
   ["-r" "--render-file FILE" "render a file as a template"
    :validate [(fn [s] true) "File doesn't exist"]] ;; todo
   ["-s" "--shell" "output as shell"]
   ["-k" "--keys" "dump the keys"]
   ["-h" "--help" "show the usage"]])

(defn usage []
  (->> cli-options
       (map #(string/join " " (take 3 %)))
       (string/join "\n")
       (println)))

(def exit-status 0)

(defn complain-value-fn [tag context-map]
  (def exit-status 1)
  (binding [*out* *err*]
    (println (format "! no key: {{%s}}" (:tag-value tag))))
  (format "{{%s}}" (:tag-value tag)))

(defn render [template theme]
  (selmer.util/set-missing-value-formatter! complain-value-fn)
  (template/render template (assoc theme :env (get-env))))

(println
 (reduce
  (fn [theme [k v]]
    ;; (println ("arg: %s" v))
    (condp = k
      :help (reduced (usage))
      :narrow (narrow v theme)
      :query (narrow v theme)
      :promote (promote v theme)
      :shell (map-to-shell theme)
      :keys (->> theme flatten-map keys (map name) (string/join "\n"))
      :render (render v theme)
      :render-file (render (slurp v) theme)
      theme))
  (get-theme)
  (:options (cli/parse-opts *command-line-args* cli-options))))

(System/exit exit-status)
