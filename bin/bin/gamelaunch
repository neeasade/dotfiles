#!/usr/bin/env bb

(ns gamelaunch
  (:require [babashka.fs :as fs]
            [clojure.string :as string]
            [lib.util :as util]))

(def game-map
  ;; build {name appid}
  (->> (fs/list-dir (util/home ".steam/steam/steamapps"))
       (filter (fn [f] (string/starts-with? (fs/file-name f) "appmanifest")))
       (keep (fn [f]
               (let [info (read-string (format "{%s}" (slurp (str f))))
                     appid (get-in info ["AppState" "appid"])
                     name  (get-in info ["AppState" "name"])]
                 (when-not (or (string/starts-with? name "Steam")
                               (string/starts-with? name "Proton"))
                   [name appid]))))
       (into {})))

(when (string/blank? (util/shh "pgrep steam"))
  (util/shh "nohup steam"))

(when-let [appid (get game-map (util/dmenu (shuffle (keys game-map))))
           ;; random game
           ;; appid (get game-map (first (shuffle (keys game-map))))
           ]
  (util/sh "steam" (str "steam://run/" appid)))
